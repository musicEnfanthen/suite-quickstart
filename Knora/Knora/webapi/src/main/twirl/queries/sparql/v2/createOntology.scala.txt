@*
 * Copyright Â© 2015-2018 the contributors (see Contributors.md).
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi.util.SmartIri
@import java.time.Instant

@*
 * Creates a new, empty ontology. If the ontology already exists, this update does nothing.
 *
 * @param triplestore the name of the triplestore being used.
 * @param ontologyNamedGraphIri the IRI of the named graph where the ontology should be stored.
 * @param ontologyIri the IRI of the ontology to be created.
 * @param projectIri the IRI of the project that the ontology belongs to.
 * @param currentTime an xsd:dateTimeStamp that will be attached to the ontology.
 *@
@(triplestore: String,
  ontologyNamedGraphIri: SmartIri,
  ontologyIri: SmartIri,
  projectIri: SmartIri,
  ontologyLabel: String,
  currentTime: Instant)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>

INSERT {
    GRAPH ?ontologyNamedGraph {
        ?ontology rdf:type owl:Ontology ;
            knora-base:attachedToProject ?project ;
            rdfs:label """@ontologyLabel"""^^xsd:string ;
            knora-base:lastModificationDate "@currentTime"^^xsd:dateTimeStamp .
    }
}
@* Ensure that inference is not used in the WHERE clause of this update. *@
@if(triplestore.startsWith("graphdb")) {
    USING <http://www.ontotext.com/explicit>
}
WHERE {
    BIND(IRI("@ontologyNamedGraphIri") AS ?ontologyNamedGraph)
    BIND(IRI("@ontologyIri") AS ?ontology)
    BIND(IRI("@projectIri") AS ?project)

    FILTER NOT EXISTS {
        ?ontology rdf:type ?existingOntologyType .
    }
}
